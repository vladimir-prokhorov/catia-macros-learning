'===========================================================================
' Переименовывание файлов в дереве.
'===========================================================================
' Замеченные недостатки:
' 1.  Неверно обрабатывает случай с многоуровневой вложенностью.
'===========================================================================
' Запланированные доработки:
'===========================================================================
' 1.  Подвязать чертеж к новому файлу модели в автоматическом режиме.
' 2.  Проработать возможность удаления или перемещения в архивную папку
'     предыдущих файлов.
'===========================================================================

Language = "VBSCRIPT"
Sub CATMain()
CATIA.DisplayFileAlerts = False
Set mainDocument = CATIA.ActiveDocument
Set rootAssembly = mainDocument.Product
sTmpPath = CATIA.ActiveDocument.Path

clrProp rootAssembly
setProp rootAssembly, 0, sTmpPath
clrProp rootAssembly
CATIA.DisplayFileAlerts = True
End Sub

Sub setProp(thisProduct, thisAsm, sTmpPath)
	Dim currAsm, currPrt
	currAsm = thisAsm
	currPrt = 0
	Set productList = thisProduct.Products
	setData thisProduct, currAsm, 0, "CATProduct", sTmpPath
	For i = 1 To productList.Count
		If (productList.Item(i).DescriptionRef <> "True" And _
				productList.Item(i).Revision <> "ANY") Then
			If (productList.Item(i).Products.Count > 0) Then
				currAsm = currAsm + 1
				setProp productList.Item(i), currAsm, sTmpPath
			Else
				currPrt = currPrt + 1
				setData productList.Item(i), thisAsm, currPrt, "CATPart", sTmpPath
			End If
		End If
	Next
End Sub

Sub setData(document, thisAsm, thisPrt, thisDfn, sTmpPath)
	document.PartNumber = "DP 4230 " & _
		String(2 - Len(thisAsm), "0") & thisAsm & _
		String(2 - Len(thisPrt), "0") & thisPrt
	document.Definition = "Definition"
	document.Revision = getDate()
	document.DescriptionRef = "True"

	MsgBox document.ReferenceProduct.Parent.Path

	document.ReferenceProduct.Parent.SaveAs(sTmpPath & "\" & document.PartNumber & " " & _
	document.Definition & " " & document.Revision & "." & thisDfn)
End Sub

Function getDate()
	SetLocale "en-us"
	Dim currDay, currMonth, currYear, mark
	currDay = Day(Date)
	currMonth = Month(Date)
	currYear = Year(Date) Mod 100
	getDate = String(2 - Len(currDay), "0") & currDay & "." & _
	 		  MonthName(currMonth,true) & "." & currYear
End Function

Sub clrProp (thisProduct)
	Set productList = thisProduct.Products
	For i = 1 To productList.Count
		productList.Item(i).DescriptionRef = ""
		clrProp productList.Item(i)
	Next
End Sub