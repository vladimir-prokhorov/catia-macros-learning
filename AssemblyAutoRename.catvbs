'================================================================================
' Принудительное переименовывание подсборок и деталей в дереве построения.
'================================================================================
' Запланированные доработки:
'================================================================================
' 1. Добавить обработчик фантомных сборок, входящим деталям которых будут
' присваиваться номера родительской сборки.
'================================================================================
Language = "VBSCRIPT"
Sub CATMain()
	CATIA.DisplayFileAlerts = False
	Set mainDocument = CATIA.ActiveDocument
	Set rootAssembly = mainDocument.Product
	sTmpPath = CATIA.ActiveDocument.Path

	clrProp rootAssembly
	setProp rootAssembly, 0, sTmpPath
	saveDoc rootAssembly
	CATIA.DisplayFileAlerts = True
End Sub
'================================================================================
Sub setProp(thisProduct, thisAsm, sTmpPath)
	Dim currAsm, currPrt
	currAsm = thisAsm
	currPrt = 0
	Set productList = thisProduct.Products
	setData thisProduct, currAsm, 0, "CATProduct", sTmpPath
	For i = 1 To productList.Count
		If (productList.Item(i).DescriptionRef <> "True" And _
				productList.Item(i).Revision <> "ANY") Then
			If (productList.Item(i).Products.Count > 0) Then
				currAsm = currAsm + 1
				setProp productList.Item(i), currAsm, sTmpPath
			Else
				currPrt = currPrt + 1
				setData productList.Item(i), thisAsm, currPrt, "CATPart", sTmpPath
			End If
		End If
	Next
End Sub
'================================================================================
Sub setData(document, thisAsm, thisPrt, thisDfn, sTmpPath)
	document.PartNumber =	"MP 7230 " & _
					String(2 - Len(thisAsm), "0") & thisAsm & _
					String(2 - Len(thisPrt), "0") & thisPrt
	document.Definition = "Definition"
	document.Revision = getDate()
	document.DescriptionRef = "True"
	set Ref = document.ReferenceProduct.Parent
	oldFullPath = Ref.FullName
	saveToRoot = True 	' Сохранить все файлы в папке основной сборки
	If (Not saveToRoot) Then sTmpPath = Ref.Path End If
	newFileName = document.PartNumber & " " & document.Definition & " " & document.Revision & "." & thisDfn
	Ref.SaveAs(sTmpPath & "\" & newFileName)
	'===========================================================================
	' Создаем папку для хранения устаревших данных
	Set oFileSys = CATIA.FileSystem
	sObsPath = sTmpPath + "\" + "Obsolete Data"
	exists = oFileSys.FolderExists(sObsPath)
	If (Not exists) Then
		Set FoldObj = oFileSys.CreateFolder(sObsPath)
	End If
	'===========================================================================
	' Выделить имя файла из полного пути
	oldFileName = Right(oldFullPath, Len(oldFullPath) - InStrRev(oldFullPath, "\"))
	sNewPath = sObsPath + "\" & oldFileName
	'===========================================================================
	' Если старый файл существует, то переместь его в архив
	If (oFileSys.FileExists(oldFullPath) And oldFileName <> newFileName) Then 
		oFileSys.CopyFile oldFullPath, sNewPath, False
		oFileSys.DeleteFile oldFullPath
	End If
	'===========================================================================
End Sub
'================================================================================
Function getDate()
	SetLocale "en-us"
	Dim currDay, currMonth, currYear, mark
	currDay = Day(Date)
	currMonth = Month(Date)
	currYear = Year(Date) Mod 100
	getDate = String(2 - Len(currDay), "0") & currDay & "." & _
	 		  MonthName(currMonth,true) & "." & currYear
End Function
'================================================================================
Sub clrProp (thisProduct)
	Set productList = thisProduct.Products
	For i = 1 To productList.Count
		productList.Item(i).DescriptionRef = ""
		clrProp productList.Item(i)
	Next
End Sub
'================================================================================
Sub saveDoc (thisProduct)
	thisProduct.ReferenceProduct.Parent.Save()
	Set productList = thisProduct.Products
	For i = 1 To productList.Count
		saveDoc productList.Item(i)
	Next
End Sub
'================================================================================